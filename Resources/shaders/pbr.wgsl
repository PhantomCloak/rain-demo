struct VertexInput {
	@location(0) a_position: vec3f,
	@location(1) a_normal: vec3f,
	@location(2) a_uv: vec2f,
	@location(3) a_tangent: vec3f,
	@location(4) a_bitangent: vec3f
};

struct InstanceInput {
	@location(5) a_MRow0: vec4<f32>,
	@location(6) a_MRow1: vec4<f32>,
	@location(7) a_MRow2: vec4<f32>,
}

struct VertexOutput {
	@builtin(position) pos: vec4f,
	@location(2) Normal: vec3f,
	@location(3) Uv: vec2f,
	@location(4) FragPos: vec3f,
	@location(5) WorldPosition: vec3f,
	@location(6) WorldNormal: vec3f,
	@location(7) WorldTangent: vec3f,
	@location(8) WorldBitangent: vec3f,
   @location(9)  ShadowCoord0: vec3f,
  @location(10) ShadowCoord1: vec3f,
  @location(11) ShadowCoord2: vec3f,
  @location(12) ShadowCoord3: vec3f,
	@location(13) Barycentric: vec3f,
};

struct SceneData {
	viewProjection: mat4x4f,
	cameraViewMatrix: mat4x4f,
	CameraPosition: vec3<f32>,
	LightDirection: vec3<f32>
};

struct ShadowData {
	ShadowViewProjection: array<mat4x4<f32>, 4>,
	CascadeDistances: vec4<f32>
};

struct MaterialUniform {
    Metallic: f32,
    Roughness: f32,
    Ao: f32,
		UseNormalMap: i32
};

@group(0) @binding(0) var<uniform> u_Scene: SceneData;


@group(1) @binding(0) var<uniform> uMaterial: MaterialUniform;
@group(1) @binding(1) var u_TextureSampler: sampler;
@group(1) @binding(2) var u_AlbedoTex: texture_2d<f32>;
@group(1) @binding(3) var u_MetallicTex: texture_2d<f32>;
@group(1) @binding(4) var u_NormalTex: texture_2d<f32>;

@group(2) @binding(0) var u_ShadowMap: texture_depth_2d_array;
@group(2) @binding(1) var u_ShadowSampler: sampler_comparison;
@group(2) @binding(2) var<uniform> u_ShadowData: ShadowData;

@group(3) @binding(0) var u_radianceMap: texture_cube<f32>;
@group(3) @binding(1) var u_radianceMapSampler: sampler;
@group(3) @binding(2) var u_BDRFLut: texture_2d<f32>;
@group(3) @binding(3) var u_irradianceMap: texture_cube<f32>;
@group(3) @binding(4) var u_irradianceMapSampler: sampler;
@group(3) @binding(5) var u_BRDFSampler: sampler;

@vertex
fn vs_main(in: VertexInput, instance: InstanceInput) -> VertexOutput {
    var out: VertexOutput;

    let transform = mat4x4<f32>(
        vec4<f32>(instance.a_MRow0.x, instance.a_MRow1.x, instance.a_MRow2.x, 0.0),
        vec4<f32>(instance.a_MRow0.y, instance.a_MRow1.y, instance.a_MRow2.y, 0.0),
        vec4<f32>(instance.a_MRow0.z, instance.a_MRow1.z, instance.a_MRow2.z, 0.0),
        vec4<f32>(instance.a_MRow0.w, instance.a_MRow1.w, instance.a_MRow2.w, 1.0)
    );

    let worldPos = transform * vec4f(in.a_position, 1.0);

    out.Normal = normalize((transform * vec4<f32>(in.a_normal, 0.0)).xyz);
    out.WorldNormal = normalize((transform * vec4<f32>(in.a_normal, 0.0)).xyz);
    out.WorldTangent = normalize((transform * vec4<f32>(in.a_tangent, 0.0)).xyz);
    out.WorldBitangent = normalize((transform * vec4<f32>(in.a_bitangent, 0.0)).xyz);

    out.WorldPosition = worldPos.xyz;
    out.Uv = in.a_uv;

    out.pos = u_Scene.viewProjection * worldPos;

    let shadowCoords0 = u_ShadowData.ShadowViewProjection[0] * worldPos;
    let shadowCoords1 = u_ShadowData.ShadowViewProjection[1] * worldPos;
    let shadowCoords2 = u_ShadowData.ShadowViewProjection[2] * worldPos;
    let shadowCoords3 = u_ShadowData.ShadowViewProjection[3] * worldPos;

    out.ShadowCoord0 = shadowCoords0.xyz / shadowCoords0.w;
    out.ShadowCoord1 = shadowCoords1.xyz / shadowCoords1.w;
    out.ShadowCoord2 = shadowCoords2.xyz / shadowCoords2.w;
    out.ShadowCoord3 = shadowCoords3.xyz / shadowCoords3.w;

		out.FragPos = (u_Scene.cameraViewMatrix * vec4f(out.WorldPosition, 1.0)).xyz;

    return out;
}

// PBR
fn GeometrySchlickGGX(NdotV: f32, roughness: f32) -> f32 {
	let r = roughness + 1.0;
	let k = (r * r) / 8.0;

	let nom = NdotV;
	let denom = NdotV * (1.0 - k) + k;

	return nom / denom;
}

fn GeometrySmith(N: vec3<f32>, V: vec3<f32>, L: vec3<f32>, roughness: f32) -> f32 {
	let NdotV = max(dot(N, V), 0.0);
	let NdotL = max(dot(N, L), 0.0);
	let ggx2 = GeometrySchlickGGX(NdotV, roughness);
	let ggx1 = GeometrySchlickGGX(NdotL, roughness);

	return ggx1 * ggx2;
}

fn getNormalFromMap(normalMap: texture_2d<f32>, defaultSampler: sampler, TexCoords: vec2<f32>, WorldPos: vec3<f32>, Normal: vec3<f32>) -> vec3<f32> {
    let tangentNormal = textureSample(normalMap, defaultSampler, TexCoords).xyz * 2.0 - 1.0;

    let Q1 = dpdx(WorldPos);
    let Q2 = dpdy(WorldPos);
    let st1 = dpdx(TexCoords);
    let st2 = dpdy(TexCoords);

    let N = normalize(Normal);
    let T = normalize(Q1 * st2.y - Q2 * st1.y);
    let B = -normalize(cross(N, T));
    let TBN = mat3x3<f32>(T, B, N);

    return normalize(TBN * tangentNormal);
}

fn GaSchlickG1(cosTheta: f32, k: f32) -> f32 {
    return cosTheta / (cosTheta * (1.0 - k) + k);
}

fn GaSchlickGGX(cosLi: f32, NdotV: f32, roughness: f32) -> f32 {
    let r = roughness + 1.0;
    let k = (r * r) / 8.0;
    return GaSchlickG1(cosLi, k) * GaSchlickG1(NdotV, k);
}

fn NdfGGX(cosLh: f32, roughness: f32) -> f32 {
	const PI: f32 = 3.141592653589793;

    let alpha = roughness * roughness;
    let alphaSq = alpha * alpha;

    let denom = (cosLh * cosLh) * (alphaSq - 1.0) + 1.0;
    return alphaSq / (PI * denom * denom);
}

fn FresnelSchlick(F0: vec3<f32>, cosTheta: f32) -> vec3<f32> {
	return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

fn FresnelSchlickRoughness(F0: vec3<f32>, cosTheta: f32, roughness: f32) -> vec3<f32> {
	return F0 + (max(vec3<f32>(1.0 - roughness), F0) - F0) * pow(1.0 - cosTheta, 5.0);
}


fn CalculateDirLights(F0: vec3<f32>, View: vec3<f32>, Normal: vec3<f32>, NdotV:f32, Albedo: vec3<f32>, Roughness: f32, Metalness: f32) -> vec3<f32> {
	var result: vec3<f32> = vec3<f32>(0.0);

	let Li: vec3<f32> = u_Scene.LightDirection;
	let Lradiance: vec3<f32> = vec3(1.0) * 1.5f;
	let Lh: vec3<f32> = normalize(Li + View);

	let cosLi: f32 = max(0.0, dot(Normal, Li));
	let cosLh: f32 = max(0.0, dot(Normal, Lh));

	let F: vec3<f32> = FresnelSchlickRoughness(F0, max(0.0, dot(Lh, View)), Roughness);
	let D: f32 = NdfGGX(cosLh, Roughness);
	let G: f32 = GaSchlickGGX(cosLi, NdotV, Roughness);

	let kd: vec3<f32> = (1.0 - F) * (1.0 - Metalness);
	let diffuseBRDF: vec3<f32> = kd * Albedo;

	const Epsilon: f32 = 1e-5;
	let specularBRDF: vec3<f32> = (F * D * G) / max(Epsilon, 4.0 * cosLi * NdotV);
	let clampedSpecularBRDF = clamp(specularBRDF, vec3<f32>(0.0), vec3<f32>(10.0));

	result += (diffuseBRDF + clampedSpecularBRDF) * Lradiance * cosLi;

	return result;
}


fn RotateVectorAboutY(angle: f32, vec: vec3<f32>) -> vec3<f32> {
    let rad = radians(angle);

    let rotationMatrix: mat3x3<f32> = mat3x3<f32>(
        vec3<f32>(cos(rad), 0.0, sin(rad)),
        vec3<f32>(0.0, 1.0, 0.0),
        vec3<f32>(-sin(rad), 0.0, cos(rad))
    );

    return rotationMatrix * vec;
}

// Shadows
fn sampleShadow(in: VertexOutput, cascadeIndex: u32, bias: f32) -> f32 {
    let shadowCoords = GetShadowMapCoords(in, cascadeIndex);
    let projCoords = shadowCoords.xy * vec2(0.5, -0.5) + vec2(0.5);
    let texelSize: vec2<f32> = vec2(1.0 / 4096.0);
    let halfKernelWidth: i32 = 1;

    var shadow: f32 = 0.0;
    let totalSamples: f32 = f32((halfKernelWidth * 2 + 1) * (halfKernelWidth * 2 + 1));

    for (var x: i32 = -halfKernelWidth; x <= halfKernelWidth; x = x + 1) {
        for (var y: i32 = -halfKernelWidth; y <= halfKernelWidth; y = y + 1) {
            let offset = vec2<f32>(f32(x), f32(y)) * texelSize;
            //let sampleCoords = projCoords + offset;
			let sampleCoords = clamp(projCoords + offset, vec2(0.0), vec2(1.0));

            let inBoundsX = step(0.0, sampleCoords.x) * (1.0 - step(1.0, sampleCoords.x));
            let inBoundsY = step(0.0, sampleCoords.y) * (1.0 - step(1.0, sampleCoords.y));
            let inBounds = inBoundsX * inBoundsY;

            let depthComparison = textureSampleCompare(
                u_ShadowMap,
                u_ShadowSampler,
                sampleCoords,
                cascadeIndex,
                shadowCoords.z - bias
            );

            let adjustedDepthComparison = inBounds * depthComparison + (1.0 - inBounds) * 1.0;

            shadow += adjustedDepthComparison;
        }
    }

    shadow /= totalSamples;
    return shadow;
}


fn IBL(F0: vec3<f32>, Lr: vec3<f32>, Normal: vec3<f32>, NdotV: f32, Albedo: vec3<f32>, Roughness: f32, Metalness: f32) -> vec3<f32> {
    let irradiance: vec3<f32> = textureSample(u_irradianceMap, u_irradianceMapSampler, Normal).rgb;

    let F: vec3<f32> = FresnelSchlickRoughness(F0, NdotV, Roughness);

    let kd: vec3<f32> = (1.0 - F) * (1.0 - Metalness);
    let diffuseIBL: vec3<f32> = Albedo * irradiance;

    let envRadianceTexLevels: u32 = textureNumLevels(u_radianceMap);

    let specularIrradiance: vec3<f32> = textureSampleLevel(
        u_radianceMap,
        u_radianceMapSampler,
        Lr,
        Roughness * f32(envRadianceTexLevels - 1u)
    ).rgb;

    let specularBRDF: vec2<f32> = textureSample(u_BDRFLut, u_BRDFSampler, vec2<f32>(NdotV, Roughness)).rg;

    let specularIBL: vec3<f32> = specularIrradiance * (F0 * specularBRDF.x + specularBRDF.y);

    return kd * diffuseIBL + specularIBL;
}

struct FragmentOutput {
    @location(0) color: vec4<f32>,
    @location(1) brightness: vec4<f32>,
}

@fragment
fn fs_main(in: VertexOutput) -> FragmentOutput  {

	// Sample PBR Resources
	let Albedo = textureSample(u_AlbedoTex, u_TextureSampler, in.Uv).rgb * uMaterial.Ao;
	let Metalness = textureSample(u_MetallicTex, u_TextureSampler, in.Uv).b * uMaterial.Metallic;
	let Roughness = textureSample(u_MetallicTex, u_TextureSampler, in.Uv).g * uMaterial.Roughness;

	// Cook our variables

	let Fdielectric = vec3(0.04);
	var Lo = vec3(0.0);

	var Normal = normalize(in.WorldNormal);

	if(uMaterial.UseNormalMap == 1)
	{
		let sampled_normal = normalize(textureSample(u_NormalTex, u_TextureSampler, in.Uv).rgb * 2.0 - 1.0);
		Normal = normalize(
				sampled_normal.x * in.WorldTangent +
				sampled_normal.y * in.WorldBitangent +
				sampled_normal.z * in.WorldNormal);
	}

	let View = normalize(u_Scene.CameraPosition - in.WorldPosition.xyz);
	let NdotV = max(dot(Normal, View), 0.0);
	let Lr = 2.0 * NdotV * Normal - View;
	//let Lr = normalize(2.0 * dot(Normal, View) * Normal - View);
	let FO = mix(Fdielectric, Albedo, Metalness);

	let lightDir = normalize(u_Scene.LightDirection);

	// Shadow Mapping

	let MIN_BIAS = 0.005;
	let bias = max(MIN_BIAS * (1.0 - dot(Normal, lightDir)), MIN_BIAS);

	let viewDepth = -in.FragPos.z;
	let SHADOW_MAP_CASCADE_COUNT = 4u;
	var layer = 0u;
	for (var i = 0u; i < SHADOW_MAP_CASCADE_COUNT - 1u; i = i + 1u) {
		if (viewDepth > u_ShadowData.CascadeDistances[i]) {
			layer = i + 1u;
		}
	}

	// Debug cascade visualization colors
	let cascadeColors = array<vec3f, 4>(
			vec3f(1.0, 0.0, 0.0),  // Red for cascade 0
			vec3f(0.0, 1.0, 0.0),  // Green for cascade 1
			vec3f(0.0, 0.0, 1.0),  // Blue for cascade 2
			vec3f(1.0, 1.0, 0.0)   // Yellow for cascade 3
			);

//#ifdef DEBUG_CASCADES
	//return vec4f(cascadeColors[layer], 1.0);
//#endif

	// Final Color
	var shadowScale = sampleShadow(in, layer, bias);

//var shadowScale = PCSS_DirectionalLight(
//    u_ShadowMap, 
//    layer, 
//    GetShadowMapCoords(in, layer),
//    10.0f  // Or whatever uniform holds your light size
//);
	shadowScale = 1.0 - clamp(1.0 - shadowScale, 0.0f, 1.0f);
	//shadowScale = 1.0;
	var lightContribution = CalculateDirLights(FO,
			View,
			Normal,
			NdotV,
			Albedo,
			Roughness,
			Metalness) * shadowScale;

	let iblContribution = IBL(FO,
			Lr,
			Normal,
			NdotV,
			Albedo,
			Roughness,
			Metalness);

   var out: FragmentOutput;
   out.color = vec4f(acesFilm(iblContribution + lightContribution), 1.0);
   out.brightness = vec4f(1.0f, 0.0f, 0.0f, 1.0f);

return out;
}

fn GetShadowMapCoords(
	in: VertexOutput,
    cascade: u32
) -> vec3<f32> {
    switch (cascade) {
        case 0: { return in.ShadowCoord0; }
        case 1: { return in.ShadowCoord1; }
        case 2: { return in.ShadowCoord2; }
        case 3: { return in.ShadowCoord3; }
        default: { return vec3<f32>(0.0); }
    }
}

fn acesFilm(x: vec3<f32>) -> vec3<f32> {
    let a = 2.51;
    let b = 0.03;
    let c = 2.43;
    let d = 0.59;
    let e = 0.14;
    return clamp((x * (a * x + b)) / (x * (c * x + d) + e), vec3<f32>(0.0, 0.0, 0.0), vec3<f32>(1.0, 1.0, 1.0));
}

fn SearchRegionRadiusUV(zWorld: f32) -> f32 {
    let light_zNear = 0.0;  // 0.01 gives artifacts? maybe because of ortho proj?
    let lightRadiusUV = 0.05;
    return lightRadiusUV * (zWorld - light_zNear) / zWorld;
}

const PoissonDistribution = array<vec2<f32>, 64>(
    vec2<f32>(-0.94201624, -0.39906216),
    vec2<f32>(0.94558609, -0.76890725),
    vec2<f32>(-0.094184101, -0.92938870),
    vec2<f32>(0.34495938, 0.29387760),
    vec2<f32>(-0.91588581, 0.45771432),
    vec2<f32>(-0.81544232, -0.87912464),
    vec2<f32>(-0.38277543, 0.27676845),
    vec2<f32>(0.97484398, 0.75648379),
    vec2<f32>(0.44323325, -0.97511554),
    vec2<f32>(0.53742981, -0.47373420),
    vec2<f32>(-0.26496911, -0.41893023),
    vec2<f32>(0.79197514, 0.19090188),
    vec2<f32>(-0.24188840, 0.99706507),
    vec2<f32>(-0.81409955, 0.91437590),
    vec2<f32>(0.19984126, 0.78641367),
    vec2<f32>(0.14383161, -0.14100790),
    vec2<f32>(-0.413923, -0.439757),
    vec2<f32>(-0.979153, -0.201245),
    vec2<f32>(-0.865579, -0.288695),
    vec2<f32>(-0.243704, -0.186378),
    vec2<f32>(-0.294920, -0.055748),
    vec2<f32>(-0.604452, -0.544251),
    vec2<f32>(-0.418056, -0.587679),
    vec2<f32>(-0.549156, -0.415877),
    vec2<f32>(-0.238080, -0.611761),
    vec2<f32>(-0.267004, -0.459702),
    vec2<f32>(-0.100006, -0.229116),
    vec2<f32>(-0.101928, -0.380382),
    vec2<f32>(-0.681467, -0.700773),
    vec2<f32>(-0.763488, -0.543386),
    vec2<f32>(-0.549030, -0.750749),
    vec2<f32>(-0.809045, -0.408738),
    vec2<f32>(-0.388134, -0.773448),
    vec2<f32>(-0.429392, -0.894892),
    vec2<f32>(-0.131597, 0.065058),
    vec2<f32>(-0.275002, 0.102922),
    vec2<f32>(-0.106117, -0.068327),
    vec2<f32>(-0.294586, -0.891515),
    vec2<f32>(-0.629418, 0.379387),
    vec2<f32>(-0.407257, 0.339748),
    vec2<f32>(0.071650, -0.384284),
    vec2<f32>(0.022018, -0.263793),
    vec2<f32>(0.003879, -0.136073),
    vec2<f32>(-0.137533, -0.767844),
    vec2<f32>(-0.050874, -0.906068),
    vec2<f32>(0.114133, -0.070053),
    vec2<f32>(0.163314, -0.217231),
    vec2<f32>(-0.100262, -0.587992),
    vec2<f32>(-0.004942, 0.125368),
    vec2<f32>(0.035302, -0.619310),
    vec2<f32>(0.195646, -0.459022),
    vec2<f32>(0.303969, -0.346362),
    vec2<f32>(-0.678118, 0.685099),
    vec2<f32>(-0.628418, 0.507978),
    vec2<f32>(-0.508473, 0.458753),
    vec2<f32>(0.032134, -0.782030),
    vec2<f32>(0.122595, 0.280353),
    vec2<f32>(-0.043643, 0.312119),
    vec2<f32>(0.132993, 0.085170),
    vec2<f32>(-0.192106, 0.285848),
    vec2<f32>(0.183621, -0.713242),
    vec2<f32>(0.265220, -0.596716),
    vec2<f32>(-0.009628, -0.483058),
    vec2<f32>(-0.018516, 0.435703)
);

const poissonDisk = array<vec2<f32>, 16>(
    vec2<f32>(-0.94201624, -0.39906216),
    vec2<f32>(0.94558609, -0.76890725),
    vec2<f32>(-0.094184101, -0.92938870),
    vec2<f32>(0.34495938, 0.29387760),
    vec2<f32>(-0.91588581, 0.45771432),
    vec2<f32>(-0.81544232, -0.87912464),
    vec2<f32>(-0.38277543, 0.27676845),
    vec2<f32>(0.97484398, 0.75648379),
    vec2<f32>(0.44323325, -0.97511554),
    vec2<f32>(0.53742981, -0.47373420),
    vec2<f32>(-0.26496911, -0.41893023),
    vec2<f32>(0.79197514, 0.19090188),
    vec2<f32>(-0.24188840, 0.99706507),
    vec2<f32>(-0.81409955, 0.91437590),
    vec2<f32>(0.19984126, 0.78641367),
    vec2<f32>(0.14383161, -0.14100790)
);

fn SamplePoisson(index: i32) -> vec2<f32> {
    return PoissonDistribution[index % 64];
}


fn FindBlockerDistance_DirectionalLight(shadowMap: texture_depth_2d_array, cascade: u32, shadowCoords: vec3<f32>, uvLightSize: f32) -> f32 {
    let bias = 0.03f;
    let numBlockerSearchSamples = 64;
    var blockers = 0;
    var avgBlockerDistance = 0.0;
    let searchWidth = SearchRegionRadiusUV(shadowCoords.z);
    let projCoords = shadowCoords.xy * vec2<f32>(0.5, -0.5) + vec2<f32>(0.5);  // Fixed coordinate transform
    
    for (var i = 0; i < numBlockerSearchSamples; i++) {
        let offset = SamplePoisson(i) * searchWidth;
        let z = textureSampleLevel(
            shadowMap,
            u_TextureSampler,
            projCoords + offset,
            cascade,
            0
        );
        
        if (z < (shadowCoords.z - bias)) {
            blockers += 1;
            avgBlockerDistance += z;
        }
    }
    if (blockers > 0) {
        return avgBlockerDistance / f32(blockers);
    }
    return -1.0;
}

fn PCF_DirectionalLight(shadowMap: texture_depth_2d_array, cascade: u32, shadowCoords: vec3<f32>, uvRadius: f32) -> f32 {
    let bias = 0.03f;
    let numPCFSamples = 64;
    var sum = 0.0;
    let projCoords = shadowCoords.xy * vec2<f32>(0.5, -0.5) + vec2<f32>(0.5);  // Fixed coordinate transform
    
    for (var i = 0; i < numPCFSamples; i++) {
        let offset = SamplePoisson(i) * uvRadius;
        let z = textureSampleLevel(
            shadowMap,
            u_TextureSampler,
            projCoords + offset,
            cascade,
            0
        );
        sum += step(shadowCoords.z - bias, z);
    }
    
    return sum / f32(numPCFSamples);
}

// PCSS function remains the same since it doesn't handle coordinates directly
fn PCSS_DirectionalLight(shadowMap: texture_depth_2d_array, cascade: u32, shadowCoords: vec3<f32>, uvLightSize: f32) -> f32 {
    let blockerDistance = FindBlockerDistance_DirectionalLight(shadowMap, cascade, shadowCoords, uvLightSize);
    if (blockerDistance == -1.0) {  // No occlusion
        return 1.0;
    }
    let penumbraWidth = (shadowCoords.z - blockerDistance) / blockerDistance;
    let NEAR = 0.01;
    var uvRadius = penumbraWidth * uvLightSize * NEAR / shadowCoords.z;
    uvRadius = min(uvRadius, 0.002);
    return PCF_DirectionalLight(shadowMap, cascade, shadowCoords, uvRadius) * 1.0f;
}
